100  REM <CTRL-J><CTRL-J>PROGRAM TITLE SCREEN<CTRL-J><CTRL-J>
110  GOTO 400
200  HTAB (22 - LEN(H$)/2): PRINT H$;: RETURN : REM  CENTER STRING
300  REM <CTRL-J><CTRL-J>TITLE STRINGS<CTRL-J>
310  VTAB 9:H$ = "L O N G E V I T Y": GOSUB 200
350  RETURN 
400  REM <CTRL-J><CTRL-J>INIT VARIBLES<CTRL-J><CTRL-J>
410  REM  SOUND ROUTINES
420  POKE 768,173: POKE 769,48: POKE 770,192: POKE 771,136: POKE 772,208: POKE 773,4: POKE 774,198: POKE 775,7: POKE 776,240: POKE 777,8: POKE 778,202: POKE 779,208: POKE 780,246: POKE 781,166: POKE 782,6: POKE 783,76: POKE 784,0: POKE 785,3: POKE 786,96: POKE 787,0:
430  REM  BORDER AND TITLES
440  HOME : INVERSE 
450  FOR H = 1 TO 19: VTAB 1: HTAB 21 -H: PRINT " ";: HTAB 20 +H: PRINT " ";: VTAB 18: HTAB 21 -H: PRINT " ";: HTAB 20 +H: PRINT " ";: NEXT 
460  FOR I = 1 TO 8: VTAB 10 -I: HTAB 1: PRINT " ";: HTAB 40: PRINT " ";: VTAB 9 +I: HTAB 1: PRINT " ";: HTAB 40: PRINT " ";: NEXT 
470  VTAB 2: HTAB 2: PRINT " ";: HTAB 39: PRINT " ": VTAB 17: HTAB 2: PRINT " ";: HTAB 39: PRINT " ";: NORMAL 
480  GOSUB 300
500  REM <CTRL-J><CTRL-J>TUNE<CTRL-J>
510  POKE 6,222: POKE 7,77: CALL 768: FOR W = 1 TO 200: NEXT : POKE 6,222: POKE 7,30: CALL 768: POKE 6,222: POKE 7,30: CALL 768: POKE 6,222: POKE 7,30: CALL 768: POKE 6,149: POKE 7,212: CALL 768
600  REM <CTRL-J><CTRL-J>STREAMER<CTRL-J>
605 Q$ = "----------------------------------------"
610 Q = 1: NORMAL : VTAB 21: HTAB 1: PRINT Q$: PRINT Q$;
620 WD$ = "                                      ... AN AWARD-WINNING PROGRAM PRESENTED BY PUBLIC DOMAIN SOFTWARE ON FILE FOR THE USE OF THE PATRONS OF THIS LIBRARY...........PRESS ANY KEY TO CONTINUE..........."
630  POKE  -16368,0:L =  LEN(WD$):SS = 60
640  FOR I = 1 TO L -1: IF  PEEK( -16384) >127  THEN  POKE  -16368,0: HOME : NORMAL : GOTO 1000
650  IF I +39 <L  THEN BN$ =  MID$ (WD$,I,40): GOTO 680
660  IF I +39 = L  THEN BN$ =  LEFT$(WD$,1) + RIGHT$(WD$,39): GOTO 680
670 BN$ =  RIGHT$(WD$,L -I +1) + LEFT$(WD$,(39 -(L -I)))
680  VTAB 22: HTAB 1: PRINT BN$
690  IF Q = 1  AND I = (L -40)  THEN Q = 0:WD$ =  RIGHT$(WD$, LEN(WD$) -40)
700  FOR WW = 1 TO SS: NEXT : NEXT : GOTO 630
710  GOTO 630
999  TEXT : HOME : VTAB 10: HTAB 6: PRINT "LOADING SELECT-A-PROGRAM MENU": PRINT : HTAB 13: PRINT "PLEASE WAIT ...":D$ =  CHR$(13) + CHR$(4): PRINT D$"RUN MENU": REM <CTRL-J><CTRL-J><CTRL-J>
1000  HOME 
1010  VTAB 9: HTAB 16: PRINT "LONGEVITY"
1020  VTAB 15: HTAB 1
1030 A$ = " "
1040  PRINT "   DO YOU WANT BACKGROUND INFORMATION?": INPUT "   (Y OR N): ";A$
1050  IF  LEFT$(A$,1) = "N"  THEN 1080
1060  HOME : GOSUB 3020: PRINT "         BACKGROUND INFORMATION"
1070  GOSUB 3020: GOSUB 3090
1080  HOME : VTAB 5: INPUT "WHAT IS YOUR NAME? ";N$
1090  PRINT : INPUT "ENTER YOUR AGE IN YEARS: ";AY:A = AY: IF A = 0  OR A >99  THEN 3270
1100  PRINT : INPUT "ARE YOU MALE OR FEMALE? (M OR F): ";SX$
1110 S$ =  LEFT$(SX$,1): IF S$ < >"M"  AND S$ < >"F"  THEN DD = 3: GOSUB 3080: GOTO 1100
1120 BA = A *.190796 +68.478510: IF  LEFT$(SX$,1) = "F"  THEN BA = A *.122769 +75.953098
1130 S$ = SX$: PRINT  CHR$(11):BA =  INT(BA +.5):T = BA
1140  PRINT "THE BASE LIFE EXPECTANCY FOR ";N$: PRINT  TAB( 9)"IS ";T;" YEARS."
1150 A$ = " "
1160  PRINT : INPUT "ARE YOU A CAUCASION? (Y OR N): ";A$
1170  IF  LEFT$(A$,1) = "N"  THEN T = T -5: GOSUB 3030: GOTO 1200
1180  IF  LEFT$(A$,1) = "Y"  THEN T = T +0: GOSUB 3030: GOTO 1200
1190 DD = 3: GOSUB 3080: GOTO 1160
1200  FOR DD = 1 TO 3000: NEXT 
1210  HOME : GOSUB 3020: PRINT "PART I      FAMILY HISTORY": GOSUB 3020: PRINT 
1220  PRINT : PRINT "DID AT LEAST TWO OF YOUR GRANDPARENTS"
1230  INPUT "LIVE TO AGE 80 OR MORE? (Y OR N): ";A$
1240  IF  LEFT$(A$,1) = "N"  THEN T = T -2: GOSUB 3030: GOTO 1270
1250  IF  LEFT$(A$,1) = "Y"  THEN T = T +2: GOSUB 3030: GOTO 1270
1260 DD = 4: GOSUB 3080: GOTO 1220
1270  PRINT : PRINT "WHAT IS/WAS YOUR MOTHER'S AGE": INPUT "IN YEARS? ";A
1280  IF A <11  THEN DD = 4: GOSUB 3080: GOTO 1270
1290  IF A <80  THEN T = T +0: GOSUB 3030: GOTO 1310
1300 T = T +1.5: GOSUB 3030
1310  PRINT : INPUT "WHAT IS/WAS YOUR FATHER'S AGE ";A
1320  IF A <11  THEN DD = 3: GOSUB 3080: GOTO 1310
1330  IF A <80  THEN T = T +0: GOSUB 3030: GOTO 1350
1340 T = T +2: GOSUB 3030
1350  PRINT : PRINT "DID ANY OF YOUR GRANDPARENTS, PARENTS,": PRINT "BROTHERS OR SISTERS DIE OF A HEART"
1360  PRINT "ATTACK OR STROKE BEFORE THE AGE OF 50?"
1370  INPUT "(Y OR N): ";A$
1380  IF  LEFT$(A$,1) = "N"  THEN T = T -0: GOSUB 3030: GOTO 1410
1390  IF  LEFT$(A$,1) = "Y"  THEN T = T -4: GOSUB 3030: GOTO 1410
1400 DD = 2: GOSUB 3080: GOTO 1370
1410  PRINT : PRINT "DID ANY OF YOUR ABOVE KIN DIE OF HEART": PRINT "ATTACK OR STROKE BEFORE THE AGE OF 60?"
1420  INPUT "(Y OR N): ";A$
1430  IF  LEFT$(A$,1) = "N"  THEN T = T -0: GOSUB 3030: GOTO 1460
1440  IF  LEFT$(A$,1) = "Y"  THEN T = T -2: GOSUB 3030: GOTO 1460
1450 DD = 2: GOSUB 3080: GOTO 1420
1460  PRINT : PRINT "WHAT IS THE TOTAL NUMBER OF INCIDENCES": PRINT "OF ASTHMA, BRONCHITIS, CANCER,"
1470  PRINT "DIABETES, DIGESTIVE SYSTEM, EMPHYSEMA": PRINT "OR THYROID DISORDER FOUND IN YOUR"
1480  INPUT "PARENTS AND GRANDPARENTS? ";A
1490  IF A <0  THEN DD = 2: GOSUB 3080: GOTO 1480
1500 T = T -3 *A: GOSUB 3030
1510  PRINT : PRINT : INPUT "HIT RETURN TO CONTINUE";Z$
1520  HOME : GOSUB 3020
1530  PRINT "PART II     HEALTH AND DIET": GOSUB 3020: PRINT 
1540  PRINT "DO YOU HAVE HIGH BLOOD PRESSURE?": INPUT "(Y OR N): ";A$
1550  IF  LEFT$(A$,1) = "Y"  AND AY =  >20  THEN T = T -10: GOSUB 3030: PRINT : GOTO 1580
1560  IF  LEFT$(A$,1) = "N"  THEN T = T +0: GOSUB 3030: PRINT : GOTO 1580
1570 DD = 3: GOSUB 3080: GOTO 1540
1580  INPUT "HOW MANY POUNDS OVERWEIGHT ARE YOU? ";A: IF A = 0  THEN B = 0: GOTO 1600
1590 B = A/10
1600 T = T -B: GOSUB 3030
1610  PRINT : PRINT "HOW MANY PACKS OF CIGARETTES DO YOU": INPUT "SMOKE EACH DAY? ";A
1620  IF A <0  THEN DD = 4: GOSUB 3080: GOTO 1610
1630  IF A = 0  THEN B = 0: GOTO 1710
1640  IF A <1  THEN B = 2: GOTO 1710
1650  IF A = 1  THEN B = 4: GOTO 1710
1660  IF A <1.5  THEN B = 5: GOTO 1710
1670  IF A <2  THEN B = 7: GOTO 1710
1680  IF A = 2  THEN B = 8: GOTO 1710
1690  IF A <3  THEN B = 10: GOTO 1710
1700  IF A =  >3  THEN B = 12: GOTO 1710
1710 T = T -B: GOSUB 3030
1720  PRINT : PRINT "HOW MANY ALCOHOLIC DRINKS DO YOU HAVE": INPUT "EACH DAY? ";A
1730  IF A <0  THEN DD = 4: GOSUB 3080: GOTO 1720
1740  IF A = 0  THEN T = T -1: GOSUB 3030: GOTO 1770
1750  IF A < = 2  THEN T = T +3: GOSUB 3030: GOTO 1770
1760  IF A >2  THEN T = T -8: GOSUB 3030: GOTO 1770
1770  PRINT : INPUT "DO YOU WANT TO KNOW WHY? ";A$
1780  IF  LEFT$(A$,1) = "N"  THEN 1840
1790  PRINT : PRINT "MODERATE DRINKING (UP TO TWO DRINKS": PRINT "PER DAY) REDUCES EMOTIONAL STRESS, BUT"
1800  PRINT "MORE THAN TWO DRINKS PER DAY PRODUCES"
1810  PRINT "PHYSIOLOGICAL DAMAGE.  AS FOR TEETO-": PRINT "TALERS, THEY MAY HAVE RATHER RIGID"
1820  PRINT "VALUE SYSTEMS AND MAY UNDERGO STRESS"
1830  PRINT "IN MAINTAINING THAT SYSTEM!"
1840  PRINT : PRINT "DO YOU EXERCISE MODERATELY - JOG, BIKE": PRINT "RIDE, TAKE LONG WALKS, SWIM - AT LEAST"
1850  INPUT "2 OR 3 TIMES EACH WEEK? ";A$
1860  IF  LEFT$(A$,1) = "N"  THEN T = T +0: GOSUB 3030: GOTO 1890
1870  IF  LEFT$(A$,1) < >"Y"  THEN DD = 2: GOSUB 3080: GOTO 1850
1880 T = T +3: GOSUB 3030
1890  PRINT : PRINT "HOW MANY HOURS DO YOU SLEEP EACH": INPUT "DAY? ";A
1900  IF A <1  OR A >24  THEN DD = 4: GOSUB 3080: GOTO 1890
1910  IF A <9  THEN T = T -0: GOSUB 3030: GOTO 1940
1920  IF A = 9  THEN T = T -4: GOSUB 3030: GOTO 1940
1930  IF A =  >9  THEN T = T -6: GOSUB 3030
1940  PRINT : INPUT "DO YOU WANT TO KNOW WHY? ";A$
1950  IF  LEFT$(A$,1) = "N"  THEN 2000
1960  PRINT : PRINT "ADULTS WHO SLEEP TOO MUCH USUALLY SPEND": PRINT "TOO MUCH TIME IN NONPHYSICAL ACTIVITY"
1970  PRINT "AND MAY BE UNHAPPY, USING SLEEP AS AN"
1980  PRINT "ESCAPE, A PSYCHOLOGICAL CRUTCH...": PRINT "DEPRESSED PEOPLE HAVE SHORTER LIFE"
1990  PRINT "EXPECTANCIES.": PRINT 
2000  PRINT 
2010  IF  LEFT$(SX$,1) = "M"  THEN 2070
2020  PRINT "DO YOU HAVE A BREAST EXAMINATION AND A": PRINT "PAP SMEAR AT LEAST ONCE A YEAR?"
2030  INPUT "(Y OR N): ";A$
2040  IF  LEFT$(A$,1) = "N"  THEN T = T +0: GOSUB 3030: GOTO 2120
2050  IF  LEFT$(A$,1) = "Y"  THEN T = T +2: GOSUB 3030: GOTO 2120
2060 DD = 2: GOSUB 3080: GOTO 2030
2070  PRINT "DO YOU HAVE A PHYSICAL AND PROCTOSCOPIC": PRINT "EXAMINATION AT LEAST ONCE EACH YEAR?"
2080  INPUT "(Y OR N): ";A$
2090  IF  LEFT$(A$,1) = "Y"  THEN T = T +2: GOSUB 3030: GOTO 2120
2100  IF  LEFT$(A$,1) = "N"  THEN T = T +0: GOSUB 3030: GOTO 2120
2110 DD = 2: GOSUB 3080: GOTO 2080
2120  PRINT : INPUT "PRESS RETURN TO CONTINUE";Z$
2130  HOME : GOSUB 3020: PRINT "PART III     EDUCATION AND OCCUPATION"
2140  GOSUB 3020
2150  PRINT : PRINT "STARTING FROM FIRST GRADE, WHAT ARE": PRINT "THE TOTAL NUMBER OF YEARS YOU ATTENDED"
2160  INPUT "SCHOOL? ";A
2170  IF A <0  OR A >AY  THEN DD = 2: GOSUB 3080: GOTO 2130
2180  IF A <12  THEN T = T -2: GOSUB 3030: GOTO 2220
2190  IF A <14  THEN T = T +0: GOSUB 3030: GOTO 2220
2200  IF A <17  THEN T = T +1: GOSUB 3030: GOTO 2220
2210  IF A =  >17  THEN T = T +3: GOSUB 3030
2220  PRINT : INPUT "DO YOU WANT TO KNOW WHY? ";A$
2230  IF  LEFT$(A$,1) = "N"  THEN 2270:
2240  PRINT : PRINT "ALTHOUGH ATTENDING SCHOOL DOES NOT MAKE": PRINT "YOU LIVE LONGER, MORE EDUCATION COR-"
2250  PRINT "RELATES WITH INCREASED INCOME, THUS"
2260  PRINT "BETTER ACCESS TO HEALTH CARE."
2270  PRINT : PRINT "IS YOUR OCCUPATION CLASSIFIED AS": INPUT "PROFESSIONAL? (Y OR N): ";A$
2280  IF  LEFT$(A$,1) = "N"  THEN 2340
2290  IF  LEFT$(A$,1) < >"Y"  THEN DD = 4: GOSUB 3080: GOTO 2270
2300  PRINT : PRINT "ARE YOU A MUSICIAN, ARCHITECT OR": INPUT "PHARMACIST? ";A$
2310  IF  LEFT$(A$,1) = "N"  THEN T = T +2: GOSUB 3030: GOTO 2380
2320  IF  LEFT$(A$,1) < >"Y"  THEN DD = 4: GOSUB 3080: GOTO 2300
2330 T = T -1: GOSUB 3030: GOTO 2380
2340  PRINT : PRINT "DOES YOUR WORK INVOLVE HEAVY INDUSTRIAL": INPUT "LABOR? ";A$
2350  IF  LEFT$(A$,1) = "N"  THEN  PRINT :T = T +0: GOSUB 3030: GOTO 2380
2360  IF  LEFT$(A$,1) < >"Y"  THEN DD = 4: GOSUB 3080: GOTO 2340
2370 T = T -2: GOSUB 3030
2380  PRINT : INPUT "DO YOU WANT TO KNOW WHY? ";A$
2390  IF  LEFT$(A$,1) = "N"  THEN  GOTO 2450
2400  PRINT : PRINT "PROFESSIONALS USUALLY LIVE LONGER...": PRINT "EXCEPT MUSICIANS, ARCHITECTS AND PHAR-"
2410  PRINT "MACISTS.  WHY THIS IS TRUE IS UNKNOWN.": PRINT "YOU HAVE A GREATER CHANCE OF BEING"
2420  PRINT "INVOLVED IN AN ACCIDENT IF YOU WORK AT"
2430  PRINT "HEAVY LABOR.  PEOPLE WHO WORK AT JOBS": PRINT "ASSOCIATED WITH OVEREATING ALSO HAVE A"
2440  PRINT "SHORTER LIFE EXPECTANCY."
2450  PRINT : PRINT "DOES YOUR ANNUAL INCOME EXCEED $50,000?": INPUT "(Y OR N): ";A$
2460  IF  LEFT$(A$,1) = "N"  THEN T = T -0: GOSUB 3030: GOTO 2490
2470  IF  LEFT$(A$,1) = "Y"  THEN T = T -2: GOSUB 3030: GOTO 2490
2480 DD = 4: GOSUB 3080: GOTO 2450
2490  PRINT : INPUT "DO YOU WANT TO KNOW WHY? ";A$
2500  IF  LEFT$(A$,1) = "N"  THEN 2530
2510  PRINT : PRINT "PEOPLE WITH HIGHER INCOMES USUALLY": PRINT "EXPERIENCE GREATER STRESS EARNING A"
2520  PRINT "LIVING AND CONSUME MORE RICH FOODS.": PRINT 
2530  PRINT : PRINT "IS YOUR JOB ACTIVE OR SEDENTARY?": INPUT "(A OR S): ";A$
2540  IF  LEFT$(A$,1) = "S"  THEN T = T -3: GOSUB 3030: GOTO 2570
2550  IF  LEFT$(A$,1) = "A"  THEN T = T +3: GOSUB 3030: GOTO 2570
2560 DD = 4: GOSUB 3080: GOTO 2530
2570  PRINT : IF AY <61  THEN 2620
2580  INPUT "ARE YOU STILL WORKING? (Y OR N): ";A$
2590  IF  LEFT$(A$,1) = "N"  THEN T = T +0: GOSUB 3030: GOTO 2620
2600  IF  LEFT$(A$,1) < >"Y"  THEN DD = 2: GOSUB 3080: GOTO 2580
2610 T = T +2: GOSUB 3030
2620  PRINT : INPUT "PRESS RETURN TO CONTINUE";Z$
2630  HOME : GOSUB 3020: PRINT "PART IV     LIFESTYLE": GOSUB 3020
2640  PRINT : PRINT "ARE YOU LIVING IN A RURAL OR URBAN": INPUT "AREA? (R OR U):";A$
2650  IF  LEFT$(A$,1) = "R"  THEN T = T +1: GOSUB 3030: GOTO 2680
2660  IF  LEFT$(A$,1) < >"U"  THEN DD = 4: GOSUB 3080: GOTO 2640
2670 T = T -1: GOSUB 3030
2680  PRINT : PRINT "ARE YOU MARRIED OR LIVING WITH SOMEONE"
2690  INPUT "ON A PERMANENT BASIS? (Y OR N): ";A$
2700  IF  LEFT$(A$,1) = "Y"  THEN T = T +3: GOSUB 3030: GOTO 2770
2710  IF  LEFT$(A$,1) < >"N"  THEN DD = 2: GOSUB 3080: GOTO 2690
2720  PRINT :A = AY -25
2730  IF A < = 0  THEN A = 0: GOTO 2760
2740  PRINT "HOW MANY OF THE PAST ";A;" YEARS WERE": INPUT "YOU SINGLE? ";A
2750 A = A/10
2760 T = T -A: GOSUB 3030
2770  PRINT : PRINT "ON A SCALE OF 0 TO 3, HOW MUCH OF A": PRINT "CALM, PASSIVE PERSON ARE YOU (3 IS THE"
2780  INPUT "MOST CALM)? ";A
2790  IF A <0  OR A >3  THEN DD = 2: GOSUB 3080: GOTO 2780
2800 T = T +A: GOSUB 3030
2810  PRINT : PRINT "ON A SCALE OF 0 TO 5, HOW MUCH OF AN": PRINT "AGGRESSIVE, INTENSE AND COMPETITIVE"
2820  PRINT "PERSON ARE YOU (5 IS THE MOST AGGRES-"
2830  INPUT "SIVE, INTENSE AND/OR COMPETITIVE)? ";A
2840  IF A <0  OR A >5  THEN DD = 2: GOSUB 3080: GOTO 2830
2850 T = T -A: GOSUB 3030
2860  PRINT : PRINT "DO YOU USE YOUR CAR'S SEAT BELTS AND"
2870  PRINT "FOLLOW SPEED LIMITS REGULARLY?": INPUT "(Y OR N): ";A$
2880  IF  LEFT$(A$,1) = "N"  THEN T = T +0: GOSUB 3030: GOTO 2910
2890  IF  LEFT$(A$,1) < >"Y"  THEN DD = 3: GOSUB 3080: GOTO 2870
2900 T = T +1: GOSUB 3030
2910  PRINT : PRINT "ON THE FOLLOWING SCALE HOW WOULD YOU": PRINT "RATE YOURSELF?": PRINT 
2920  PRINT "SAD  -3  -2  -1  0  +1  +2  +3  HAPPY"
2930  PRINT  TAB( 17);: INPUT A
2940  IF A < -3  OR A >3  THEN DD = 2: GOSUB 3080: GOTO 2930
2950  IF A =  >0  THEN T = T +A: GOSUB 3030: GOTO 2970
2960 T = T +A: GOSUB 3030
2970  PRINT : INPUT "HIT RETURN TO CONTINUE";Z$: HOME 
2980  GOSUB 3020: GOSUB 3030: GOSUB 3020
2990  PRINT : PRINT : PRINT : PRINT : INPUT "DO YOU WANT TO RUN ANOTHER LIFE         EXPECTANCY PROFILE? (Y OR N): ";Z$
3000  IF  LEFT$(Z$,1) = "Y"  THEN 1000: PRINT 
3010  PRINT : PRINT  TAB( 10)"GOOD LUCK ";N$: FOR W = 1 TO 2500: NEXT : GOTO 999
3020  FOR X = 1 TO 38: PRINT "-";: NEXT X: PRINT "-": RETURN 
3030  PRINT : INVERSE : PRINT  TAB( 41);: PRINT " "N$","; TAB( 41);: PRINT " YOUR LIFE EXPECTANCY IS NOW "T" YEARS."; TAB( 41);: PRINT  TAB( 41);: NORMAL 
3040  IF AY < = T  THEN A$ = " ": RETURN 
3050  PRINT : PRINT "YOU MUSTN'T TRY TO FOOL MOTHER NATURE...."
3060  PRINT  TAB( 10)"CHECK YOUR ANSWERS!!!"
3070  PRINT : PRINT "RESTART PROGRAM FROM THE BEGINNING.";: GOTO 2990
3080  PRINT : PRINT "PLEASE CHECK YOUR ANSWER!!!": FOR XX = 1 TO 2000: NEXT : VTAB  PEEK(37) -DD: CALL  -958: RETURN 
3090  PRINT : PRINT "     HUMAN BEINGS ARE OFTEN PREOCCUPIED"
3100  PRINT "WITH THE PROSPECTS FOR LONG LIFE AS IN-": PRINT "DICATED BY THE CURRENT POPULARITY OF"
3110  PRINT "ASTROLOGY, BIORHYTHMS, NUTRITION AND"
3120  PRINT "HEALTH CARE PROGRAMS.  SCIENTISTS HAVE": PRINT "DEVELOPED A VARIETY OF METHODS TO ESTI-"
3130  PRINT "MATE HUMAN LONGEVITY.  STATISTICIANS"
3140  PRINT "HAVE CREATED MORTALITY TABLES BASED": PRINT "UPON HUMAN POPULATION STUDIES, AND MED-"
3150  PRINT "ICAL EXPERTS HAVE IDENTIFIED THE REQUI-": PRINT "SITE CRITERIA FOR THE MAINTENANCE OF"
3160  PRINT "GOOD HEALTH."
3170  PRINT "     VITAL STATISTICS COMPUTED BY THE": PRINT "U.S. PUBLIC HEALTH SERVICE FROM 1960"
3180  PRINT "AND 1970 CENSUS DATA ARE UTILIZED IN"
3190  PRINT "THIS PROGRAM TO ESTIMATE YOUR BASE LIFE": PRINT "EXPECTANCY."
3200  PRINT : INPUT "HIT RETURN FOR NEXT PAGE";Z$: HOME 
3210  PRINT "     YOUR LIFE EXPECTANCY DEPENDS ON": PRINT "MANY FACTORS INCLUDING PLACE OF RESI-"
3220  PRINT "DENCE, MOBILITY STATUS, NATIVITY, FAM-"
3230  PRINT "ILY HISTORY, RACE AND EDUCATION IN ADD-": PRINT "ITION TO PHYSICAL AND EMOTIONAL CONSID-"
3240  PRINT "ERATIONS.  YOUR RESPONSES TO QUESTIONS"
3250  PRINT "REGARDING THESE FACTORS FORM THE BASIS": PRINT "FOR YOUR PROJECTED LONGEVITY."
3260  PRINT : INPUT "HIT RETURN TO START THE PROGRAM";Z$: HOME : RETURN 
3270  PRINT 
3280  PRINT "SORRY!  THIS PROGRAM CANNOT PROJECT THE": PRINT "LIFE EXPECTANCY OF YOUR STATED AGE."
3290  PRINT "TRY YOUR REAL AGE THIS TIME!"
3300  PRINT 
3310  FOR XX = 1 TO 3000: NEXT : VTAB  PEEK(37) -6: CALL  -958
3320  GOTO 1090
60660  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>          <CTRL-M><CTRL-M>   -------- NOTICE --------<CTRL-M><CTRL-M> ALL ENHANCEMENTS TO THIS PRO-<CTRL-M>GRAM   ARE   COPYRIGHTED  1985<CTRL-M>PUBLIC  DOMAIN RESEARCH.  ANY-<CTRL-M>ONE WISHING TO DISTRIBUTE THIS<CTRL-M>MODIFIED  PROGRAM   MUST  HAVE
63999  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>THE EXPRESSED WRITTEN  CONSENT<CTRL-M>OF THE  PUBLISHER.  LEGITIMATE<CTRL-M>USER GROUPS  MAY  REQUEST THIS<CTRL-M>PERMISSION.<CTRL-M><CTRL-M>    ----------//----------<CTRL-M>