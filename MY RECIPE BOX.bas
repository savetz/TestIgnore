0  GOSUB 10000
1  GOSUB 9000
2  REM <CTRL-M><CTRL-M>MY RECIPE BOX<CTRL-M><CTRL-M><CTRL-M>
10 DD$ =  CHR$(13) + CHR$(4):D$ =  CHR$(4)
20 OP$ = DD$ +"OPEN ":RD$ = D$ +"READ ":WR$ = D$ +"WRITE ":CL$ = D$ +"CLOSE "
30  DIM IN$(52),I$(52)
35  GOTO 110
40  HOME : SPEED= 150
50  VTAB 8: PRINT "DO YOU WANT TO ERASE ALL THE RECIPES    THAT ARE STORED ON THIS DISK?  (Y/N) ": GET A$: CALL  -958
60  IF A$ = "Y"  THEN  PRINT : PRINT "ARE YOU SURE?": PRINT : PRINT "--> ANSWERING 'Y' WILL ERASE ALL FILES.": PRINT : PRINT "--> ANSWERING 'N' WILL TAKE YOU TO THE      MENU.": GET A$: PRINT 
70  SPEED= 255
80  IF A$ = "Y"  THEN  GOSUB 6500: GOTO 110
90  IF A$ = "N"  THEN  GOTO 110
100  GOSUB 7000: GOTO 50
110  HOME : PRINT  TAB( 10);: INVERSE : PRINT "RECIPE FILE": NORMAL : PRINT 
120  PRINT "OPTIONS ARE:": PRINT 
125  PRINT  TAB( 5)"1  VIEW RECIPES": PRINT 
130  PRINT  TAB( 5)"2  ENTER NEW RECIPE": PRINT 
140  PRINT  TAB( 5)"3  EDIT EXISTING RECIPE": PRINT 
150  PRINT  TAB( 5)"4  PRINT RECIPE ON PRINTER": PRINT 
160  PRINT  TAB( 5)"5  ADD TO EXISTING RECIPE": PRINT 
170  PRINT  TAB( 5)"6  INITIALIZE RECIPE BOX": PRINT 
175  PRINT  TAB( 5)"7  EXIT PROGRAM": PRINT 
180  PRINT "ENTER SELECTION NUMBER: ";: GET A$:A =  VAL(A$): PRINT A: PRINT 
190  IF A <1  OR A >7  THEN  INVERSE : PRINT  TAB( 41);: PRINT "     NUMBER NOT IN ACCEPTABLE RANGE.    ";: PRINT  TAB( 41);: NORMAL : FOR W = 1 TO 2000: NEXT : GOTO 110
195  IF A = 1  THEN IO = 0
196  IF A = 4  THEN IO = 1
200  ON A GOTO 2010,500,1500,2000,2500,40,210
210  PRINT "BON APPETIT!": GOTO 19999
449  REM   ENTER NEW RECIPE
500  HOME 
509  FOR I = 1 TO 50:IN$(I) = "":I$(I) = "": NEXT 
510  HOME : PRINT  TAB( 10);: INVERSE : PRINT "ENTER NEW RECIPE": NORMAL : PRINT 
520  PRINT "NAME OF RECIPE SHOULD BE LIMITED TO 30  CHARACTERS.": PRINT 
530  PRINT "ENTER NAME OF RECIPE -": INPUT FI$: IF FI$ = ""  THEN 110
540  IF  LEN(FI$) >30  THEN  PRINT : INVERSE : PRINT "PLEASE LIMIT NAME TO 30 CHARACTERS.": NORMAL : PRINT : PRINT : GOTO 530
550  PRINT 
560  PRINT "ENTER TYPE OF RECIPE (DESSERT MISC.,    CAKE, CANDY, PIE, MAIN DISH, SALAD AND  DRESSING, SAUCE, VEGETABLE, DRINK, SOUP,BREAD, APPETIZER, MISC.)."
570  PRINT : INPUT "TYPE IS - ";TY$
580  PRINT : PRINT 
590  PRINT "RECIPE NAME IS - ": PRINT  TAB( 3);: INVERSE : PRINT FI$: NORMAL 
600  PRINT : PRINT "RECIPE TYPE IS - ": PRINT  TAB( 3);: INVERSE : PRINT TY$: NORMAL 
610  PRINT : PRINT "IS THIS CORRECT (Y OR N)? ";: GET A$: PRINT A$: PRINT 
620  IF A$ = "N"  THEN  PRINT : GOTO 530
630  IF A$ = "Y"  THEN  GOSUB 3000: GOSUB 5500: GOTO 1000
640  PRINT : PRINT "PLEASE ANSWER 'Y' OR 'N'.": PRINT : GOTO 610
999  REM    ENTER RECIPE
1000  HOME : INVERSE : PRINT " "TY$" ": PRINT : PRINT " "FI$" ": NORMAL : PRINT : PRINT 
1020  PRINT  TAB( 13);: INVERSE : PRINT "ENTER RECIPE": NORMAL : PRINT 
1030  PRINT "ENTER INGREDIENTS AND INSTRUCTIONS ONE  LINE (60 CHARACTERS MAXIMUM) AT A TIME. INGREDIENTS AND INSTRUCTIONS MAY BE     ENTERED IN ANY FORM THAT IS CONVENIENT  FOR YOU. ENTER RECIPE EXACTLY AS YOU    WANT IT TO PRINT OUT."
1040  INVERSE : PRINT "DO NOT USE COMMAS OR COLONS!!!": NORMAL : PRINT 
1050  FOR I = 2 TO 50
1060  INPUT IN$(I)
1070  IF  LEN(IN$(I)) < = 1  THEN N = I -1: GOTO 1100
1080  IF  LEN(IN$(I)) >60  THEN  PRINT : PRINT "LENGTH IS LIMITED TO 60 CHARACTERS.     PLEASE RE-ENTER LINE.": GOTO 1060
1090  NEXT 
1100  HOME : INVERSE : PRINT TY$: NORMAL : PRINT  TAB( 3);: INVERSE : PRINT FI$: NORMAL : PRINT 
1110  PRINT "RECIPE WILL PRINT OUT ONE LINE AT A     TIME. IF THE LINE IS CORRECT, PRESS     'RETURN' FOR THE NEXT LINE. IF THE LINE IS WRONG, RE-ENTER LINE AFTER PROMPT    THEN PRESS 'RETURN'.  RECIPE CAN BE     PRINTED OUT 60 COLUMNS WIDE"
1120  FOR I = 2 TO N
1130  PRINT IN$(I)
1140  INPUT I$(I)
1150  IF  LEN(I$(I)) < = 1  THEN 1180
1160  IF  LEN(I$(I)) >60  THEN  PRINT : PRINT "LENGTH IS LIMITED TO 60 CHARACTERS.     PLEASE RE-ENTER LINE.": GOTO 1140
1170 IN$(I) = I$(I)
1180  NEXT 
1190  HOME : INVERSE : PRINT TY$: NORMAL : PRINT  TAB( 3);: INVERSE : PRINT FI$: NORMAL 
1200  PRINT 
1210  SPEED= 150
1220  FOR I = 2 TO N: PRINT IN$(I): NEXT 
1230  SPEED= 255
1240  PRINT 
1250  PRINT "IS THE RECIPE CORRECT (Y OR N)? ";: GET A$: PRINT A$: PRINT 
1260  IF A$ = "N"  THEN 1100
1270  IF A$ = "Y"  THEN  GOSUB 3500: GOTO 110
1280  INVERSE : PRINT "PLEASE ANSWER 'Y' OR 'N':NORMAL:PRINT:GOTO 990
1499  REM    EDITING ROUTINE
1500  HOME : PRINT : PRINT : PRINT  TAB( 10)"EDIT EXISTING RECIPE": VTAB 7
1509  HOME : PRINT  TAB( 8);: INVERSE : PRINT "EDIT EXISTING RECIPE": NORMAL : PRINT 
1510  PRINT : PRINT "TYPE ";
1512  FLASH 
1514  PRINT "HELP";
1516  NORMAL 
1518  PRINT " TO SEE A LIST OF THE RECIPES."
1519  PRINT : PRINT "----------------------------------------": PRINT : PRINT 
1520  VTAB 8: PRINT "ENTER NAME OF RECIPE - ": INPUT FI$: IF FI$ = ""  THEN 110
1530  IF FI$ = "HELP"  THEN  GOSUB 5000: PRINT : PRINT : GOTO 1520
1540  GOSUB 4000
1550  HOME : PRINT  TAB( 10);: INVERSE : PRINT "EDIT A RECIPE": NORMAL : PRINT : PRINT : INVERSE : PRINT FI$: NORMAL : PRINT 
1560  PRINT "IF ITEM IS CORRECT, PRESS 'RETURN' FOR  THE NEXT LINE. THIS WILL STEP THROUGH   ALL ITEMS IN THE RECIPE."
1570  FOR I = 1 TO Z
1580  PRINT IN$(I)
1590  INPUT I$(I): IF  LEN(I$(I)) < = 1  THEN 1620
1600  IF  LEN(IN$(I)) >60  THEN  PRINT : PRINT "LENGTH IF LIMITED TO 60 CHARACTERS.   PLEASE RE-ENTER LINE.": GOTO 1590
1610 IN$(I) = I$(I)
1620  NEXT 
1630  PRINT 
1640  PRINT "ARE ALL CORRECTIONS NOW MADE(Y OR N)? ";: GET A$: PRINT A$: PRINT 
1650  IF A$ = "N"  THEN 1550
1660  IF A$ = "Y"  THEN  GOSUB 4500: GOTO 110
1670  INVERSE : PRINT "PLEASE ANSWER 'Y' OR 'N'.": NORMAL : PRINT : GOTO 1640
1999  REM   PRINT ROUTINE
2000  HOME : PRINT  TAB( 12): INVERSE : PRINT "PRINTOUT RECIPE": NORMAL : PRINT : GOTO 2012
2010  HOME : PRINT  TAB( 14);: INVERSE : PRINT "VIEW RECIPE": NORMAL : PRINT 
2012  PRINT : PRINT "TYPE ";
2013  FLASH 
2014  PRINT "HELP";
2015  NORMAL 
2016  PRINT " TO SEE LIST OF RECIPES."
2017  PRINT : PRINT "----------------------------------------": PRINT : PRINT 
2020  PRINT "ENTER RECIPE NAME - ": INPUT FI$: IF FI$ = ""  THEN 110
2030  IF FI$ = "HELP"  THEN  GOSUB 5000: PRINT : PRINT : GOTO 2020
2035  PRINT : PRINT : PRINT 
2040  GOSUB 4000
2050  PRINT : PRINT D$;"PR#"IO: PRINT 
2055  HOME 
2060  PRINT "RECIPE TYPE - ";IN$(1): PRINT 
2070  PRINT "RECIPE NAME - ";FI$: PRINT 
2080  IF IO = 1  THEN  FOR J = 1 TO 59: PRINT "*";: NEXT : PRINT 
2090  FOR I = 2 TO Z
2100  PRINT IN$(I)
2110  NEXT 
2120  IF IO = 1  THEN  FOR J = 1 TO 59: PRINT "*";: NEXT : PRINT 
2130  PRINT D$;"PR#0":
2140  VTAB 24: HTAB 1: PRINT "PRINT OUT ANOTHER RECIPE? (Y/N)";: GET A$
2150  IF A$ = "N"  THEN  GOTO 110
2160  IF A$ = "Y"  THEN  GOTO 2000
2170  PRINT "<CTRL-G><CTRL-G>";: GOTO 2140
2499  REM   ADD TO EXISTING RECIPE
2500  HOME : PRINT : PRINT : PRINT  TAB( 9)"ADD TO EXISTING RECIPE": VTAB 7
2509  HOME : PRINT  TAB( 9);: INVERSE : PRINT "ADD TO EXISTING RECIPE": NORMAL : PRINT 
2510  FOR I = 1 TO 50:IN$(I) = "":I$(I) = "": NEXT 
2521  PRINT : PRINT "TYPE ";
2522  INVERSE 
2523  PRINT "HELP";
2524  NORMAL : PRINT " TO SEE LIST OF RECIPES."
2525  PRINT : PRINT : PRINT "PRESS <RETURN> TO RETURN TO MENU.": PRINT : PRINT 
2526  PRINT "----------------------------------------": PRINT : PRINT 
2530  PRINT "ENTER RECIPE NAME - ": INPUT FI$
2540  IF  LEN(FI$) < = 1  THEN 110
2550  IF FI$ = "HELP"  THEN  GOSUB 5000: PRINT : PRINT : GOTO 2530
2560  GOSUB 4000
2570  IF Z <50  THEN 2620
2580  IF Z = 50  THEN  PRINT : PRINT "FILE IS FULL. DO YOU WANT TO EDIT THIS  FILE (Y OR N)? ";: GET A$: PRINT A$: PRINT 
2590  IF A$ = "Y"  THEN 1500
2600  IF A$ = "N"  THEN 110
2610  GOSUB 7000: GOTO 2580
2620  HOME : INVERSE : PRINT FI$: NORMAL : PRINT : PRINT 
2630  PRINT "ENTER ADDITIONAL LINES ONE AT A TIME.   A MAXIMUM OF 50 LINES MAY APPEAR IN THE RECIPE. PRESS 'RETURN' TO STOP ENTERING.": PRINT 
2640  FOR I = 1 TO 1000: NEXT 
2650  SPEED= 150
2660  FOR I = 1 TO Z: PRINT IN$(I): NEXT 
2670 Z1 = Z +1
2680  SPEED= 255
2690  FOR I = Z1 TO 50
2700  INPUT IN$(I)
2710  IF  LEN(IN$(I)) < = 1  THEN N = I -1: GOTO 2740
2720  IF  LEN(IN$(I)) >60  THEN  PRINT : PRINT "LENGTH IS LIMITED TO 60 CHARACTERS.   PLEASE RE-ENTER.": GOTO 2700
2730  NEXT 
2740  HOME : INVERSE : PRINT FI$: NORMAL : PRINT : PRINT 
2750  PRINT "THIS ALLOWS YOU TO CHECK YOUR TYPING. IFA LINE IS CORRECT, PRESS 'RETURN' AND   THE NEXT LINE WILL BE DISPLAYED. MAKE   ANY CORRECTIONS AFTER THE PROMPT APPEARSTHEN PRESS 'RETURN' FOR THE NEXT LINE."
2760  PRINT 
2770  FOR I = Z1 TO N
2780  PRINT IN$(I)
2790  INPUT I$(I)
2800  IF  LEN(I$(I)) < = 1  THEN 2830
2810  IF  LEN(I$(I)) >60  THEN  PRINT : PRINT "LENGTH IS LIMITED TO 60 CHARACTERS.   PLEASE RE-ENTER.": PRINT : GOTO 2790
2820 IN$(I) = I$(I)
2830  NEXT 
2840  HOME : INVERSE : PRINT FI$: NORMAL : PRINT : PRINT 
2850  SPEED= 150
2860  FOR I = 1 TO N: PRINT IN$(I): NEXT 
2870  PRINT 
2880  SPEED= 255
2890  PRINT "ARE ALL CORRECTIONS MADE (Y OR N)? ";: GET A$: PRINT A$: PRINT 
2900  IF A$ = "Y"  THEN Z = N: GOSUB 4500: GOTO 110
2910  IF A$ = "N"  THEN 2740
2920  GOSUB 7000: GOTO 2890
2999  REM   CREATE RECIPE FILE
3000  HOME : VTAB 12: PRINT "PLEASE WAIT WHILE I CREATE A FILE FOR   THIS RECIPE."
3010  PRINT OP$;FI$", L60"
3020  PRINT WR$;FI$", R0": PRINT 1
3030  PRINT WR$;FI$", R1": PRINT TY$
3040  PRINT CL$
3050  RETURN 
3499  REM    ADD INGREDIENTS AND INSTRUCTIONS TO FILE
3500  HOME : VTAB 12: PRINT "PLEASE WAIT WHILE I STORE THE RECIPE ON DISK."
3510  PRINT OP$;FI$;", L60"
3520  PRINT RD$;FI$;",R0"
3530  INPUT Z
3540 Z = Z +1
3550  FOR I = Z TO N
3560  PRINT WR$;FI$;", R";I
3570  PRINT IN$(I)
3580  NEXT I
3590 Z = (N +Z) -2
3600  PRINT WR$;FI$;" ,R0": PRINT Z
3610  PRINT CL$
3620  RETURN 
3630  REM   LOAD RECIPE FILE FROM DISK
3999  REM    LOAD RECIPE FILE FROM DISK
4000  GOSUB 6000
4010  FOR I = 1 TO Z: IF FI$ = RR$(I)  THEN 4050
4011  NEXT 
4020  VTAB 12: INVERSE : PRINT FI$;" WAS NOT FOUND ON RECORD.": NORMAL : PRINT : PRINT 
4030  INVERSE : PRINT "PLEASE CHECK YOUR SPELLING.": NORMAL : PRINT : PRINT 
4040  INVERSE : PRINT "A LIST OF RECIPES CURRENTLY ON DISK IS  AVAILABLE BY TYPING 'HELP'.": NORMAL : FOR W = 1 TO 2999: NEXT : RETURN 
4050  VTAB 18: PRINT "  PLEASE WAIT WHILE I LOAD THE RECIPE."
4060  PRINT OP$;FI$;", L60"
4070  PRINT RD$;FI$;",R0": INPUT Z
4080  FOR I = 1 TO Z
4090  PRINT RD$;FI$;", R";I
4100  INPUT IN$(I)
4110  NEXT 
4120  PRINT CL$
4130  RETURN 
4499  REM    PRINT CORRECTED RECIPE BACK TO DISK
4500  HOME : VTAB 12: PRINT "I AM NOW WRITING THE CORRECTED RECIPE   BACK TO THE DISK. PLEASE WAIT."
4510  PRINT OP$;FI$;", L60"
4520  FOR I = 1 TO Z
4530  PRINT WR$;FI$;", R";I
4540  PRINT IN$(I)
4550  NEXT 
4560  PRINT WR$;FI$;", R0": PRINT Z
4570  PRINT CL$
4580  RETURN 
4999  REM    'HELP' ROUTINE
5000  GOSUB 6000
5010  HOME : PRINT  TAB( 8);: INVERSE : PRINT "RECIPES ON FILE": NORMAL : PRINT 
5020 C = 2
5030  FOR I = 1 TO Z
5040  IF C = 22  THEN  VTAB 24: INVERSE : PRINT "PRESS 'RETURN' TO CONTINUE.";: GET A$: HOME : PRINT  TAB( 8);: INVERSE : PRINT "RECIPES ON FILE": NORMAL : PRINT :C = 2
5050  PRINT RR$(I)
5060 C = C +1
5070  NEXT 
5080  RETURN 
5499  REM    PRINTS RECIPE NAMES TO RECIPE RECORD FILE
5500  HOME : VTAB 12: PRINT " PLEASE WAIT WHILE I UPDATE THE RECIPE   RECORD ..."
5505  PRINT OP$;"RECIPE RECORD, L30"
5510  PRINT RD$;"RECIPE RECORD, R0": INPUT Z
5520 Z = Z +1
5525  PRINT CL$
5526  PRINT OP$"RECIPE RECORD,L30"
5530  PRINT WR$;"RECIPE RECORD, R";Z: PRINT FI$
5540  PRINT WR$;"RECIPE RECORD, R0": PRINT Z
5550  PRINT CL$
5560  RETURN 
5999  REM    READ RECIPE RECORD FILE FROM DISK
6000  PRINT OP$;"RECIPE RECORD, L30"
6010  PRINT RD$;"RECIPE RECORD, R0": INPUT Z
6020  FOR I = 1 TO Z
6030  PRINT RD$;"RECIPE RECORD,R"I
6040  INPUT RR$(I)
6050  NEXT 
6060  PRINT CL$
6070  RETURN 
6499  REM    CREATE RECIPE RECORD FILE
6500  HOME : VTAB 12: PRINT "ONE MOMENT ... INITIALIZING THIS PROGRAM"
6510  PRINT OP$;"RECIPE RECORD, L30"
6520  PRINT WR$;"RECIPE RECORD, R0": PRINT 0
6530  PRINT CL$
6540  RETURN 
6999  REM    NOT 'Y' OR 'N' ROUTINE 
7000  PRINT : PRINT : INVERSE : PRINT "PLEASE ANSWER 'Y' OR 'N'.": NORMAL : PRINT : PRINT : RETURN 
7499  REM    PRINT ADDITIONS TO FILE
7500  VTAB 12: PRINT "PLEASE WAIT WHILE I PRINT THESE         ADDITIONS TO THE DISK."
7510  PRINT OP$;FI$;", L60"
7520  PRINT WR$;FI$;", R0": PRINT N
7530  FOR I = Z TO N
7540  PRINT WR$;FI$;",R";I: PRINT IN$(I)
7550  NEXT 
7560  PRINT CL$
7570  RETURN 
9000  HOME 
9010  VTAB 3: HTAB 16: PRINT "---------": HTAB 16: PRINT "IMPORTANT": HTAB 16: PRINT "---------"
9020  VTAB 7: HTAB 4: PRINT "1. DOES THIS DISKETTE CONTAIN ANY          OTHER FILES BESIDES THIS                PROGRAM AND ITS SUPPORT FILES?"
9030  PRINT : HTAB 4: PRINT "2. IS THIS DISKETTE WRITE-PROTECTED?       (THAT IS, DOES IT HAVE A TAB ON         ITS WRITE-PROTECT NOTCH?)"
9040  PRINT : PRINT : HTAB 4: PRINT "TYPE 'Y' IF THE ANSWER IS 'YES' TO      EITHER OF THE ABOVE TWO QUESTIONS."
9050  PRINT : HTAB 4: PRINT "PRESS ANY OTHER KEY IF THE ANSWER       IS 'NO' TO BOTH QUESTIONS. ";
9060  GET Z$
9070  IF Z$ < >"Y"  THEN  RETURN 
9080  HOME : VTAB 2: HTAB 9: PRINT "WARNING TO NOVICE USERS"
9090  PRINT : PRINT "THIS PROGRAM CAN CREATE OR MODIFY OTHER PROGRAMS AND FILES."
9100  PRINT : PRINT "THIS COULD POSSIBLY ERASE OR OTHERWISE  DAMAGE OTHER PROGRAMS ON THIS DISKETTE."
9110  PRINT : PRINT "TO BE SAFE, COPY THIS PROGRAM (AND ITS  SUPPORT FILES) ONTO ITS OWN FORMATTED,  BLANK DISKETTE.  DO NOT COPY ANY OTHER  FILES ONTO IT."
9120  VTAB 23: PRINT "PRESS ANY KEY TO CONTINUE...";: GET Z$
9130  HOME : PRINT : HTAB 14: PRINT "INSTRUCTIONS"
9140  PRINT : PRINT "FIRST, FORMAT A BLANK DISKETTE.  THEN,  COPY THESE PROGRAMS AND FILES ONTO IT:"
9150  PRINT : HTAB 14: PRINT "MY RECIPE BOX                           RECIPE RECORD                           CHICKEN SOUP"
9160  PRINT : PRINT "TO COPY THOSE FILES, USE APPLE'S FILEM  OR FID UTILITY."
9170  PRINT : PRINT "DO NOT WRITE-PROTECT YOUR NEW DISKETTE. OTHERWISE, THIS PROGRAM MIGHT CRASH     BECAUSE IT WON'T BE ABLE TO CREATE OR   MODIFY THE NECESSARY FILES."
9180  VTAB 22: PRINT "() TYPE 'Q' TO QUIT THIS PROGRAM NOW.   () PRESS ANY OTHER KEY TO RUN IT.";: GET Z$
9190  IF Z$ = "Q"  THEN 19999
9200  RETURN 
10000  REM <CTRL-J><CTRL-J>PROGRAM TITLE<CTRL-J>
10010  GOTO 10040
10020  HTAB (22 -( LEN(H$)/2)): PRINT H$: RETURN 
10030  VTAB (7):H$ = "M Y   R E C I P E   B O X": GOSUB 10020
10035  VTAB (11):H$ = "BY JUDD ELLMERS": GOSUB 10020: RETURN 
10040  POKE 768,173: POKE 769,48: POKE 770,192: POKE 771,136: POKE 772,208: POKE 773,4: POKE 774,198: POKE 775,7: POKE 776,240: POKE 777,8: POKE 778,202: POKE 779,208: POKE 780,246: POKE 781,166: POKE 782,6: POKE 783,76: POKE 784,0: POKE 785,3: POKE 786,96: POKE 787,0
10050  HOME : INVERSE : FOR H = 1 TO 19: VTAB 1: HTAB 21 -H: PRINT " ";: HTAB 20 +H: PRINT " ";: VTAB 18: HTAB 21 -H: PRINT " ";: HTAB 20 +H: PRINT " ";: NEXT 
10060  FOR I = 1 TO 8: VTAB 10 -I: HTAB 1: PRINT " ";: HTAB 40: PRINT " ";: VTAB 9 +I: HTAB 1: PRINT " ";: HTAB 40: PRINT " ";: NEXT 
10070  VTAB 2: HTAB 2: PRINT " ";: HTAB 39: PRINT " ": VTAB 17: HTAB 2: PRINT " ";: HTAB 39: PRINT " ";: NORMAL 
10080  GOSUB 10030
10090  POKE 6,222: POKE 7,77: CALL 768: FOR W = 1 TO 200: NEXT : POKE 6,222: POKE 7,30: CALL 768: POKE 6,222: POKE 7,30: CALL 768: POKE 6,222: POKE 7,30: CALL 768: POKE 6,149: POKE 7,212: CALL 768
10100 WD$ = "                                        ... AN AWARD-WINNING PROGRAM PRESENTED BY PUBLIC DOMAIN SOFTWARE ON FILE FOR THE USE OF THE PATRONS OF THIS LIBRARY ............. PRESS ANY KEY TO CONTINUE .........."
10110 Q$ = "----------------------------------------":Q = 1: NORMAL : VTAB 21: HTAB 1: PRINT Q$: PRINT Q$;: POKE  -16368,0:L =  LEN(WD$):W = 75
10120  IF  PEEK( -16384) >127  THEN  POKE  -16368,0: HOME : NORMAL : RETURN 
10130 BN$ =  LEFT$(WD$,40): IF L >166  THEN WD$ =  MID$ (WD$,2,L -1):L = L -1: GOTO 10150
10140 WD$ =  MID$ (WD$,2,L -1) + LEFT$(WD$,1)
10150  VTAB 22: HTAB 1: PRINT BN$: FOR WW = 1 TO W: NEXT : GOTO 10120
19999  TEXT : HOME : VTAB 10: HTAB 6: PRINT "LOADING SELECT-A-PROGRAM MENU": PRINT : HTAB 13: PRINT "PLEASE WAIT ...":D$ =  CHR$(13) + CHR$(4): PRINT D$"RUN MENU"
60660  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>          <CTRL-M><CTRL-M>   -------- NOTICE --------<CTRL-M><CTRL-M> ALL ENHANCEMENTS TO THIS PRO-<CTRL-M>GRAM   ARE   COPYRIGHTED  1985<CTRL-M>PUBLIC  DOMAIN RESEARCH.  ANY-<CTRL-M>ONE WISHING TO DISTRIBUTE THIS<CTRL-M>MODIFIED  PROGRAM   MUST  HAVE
63999  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>THE EXPRESSED WRITTEN  CONSENT<CTRL-M>OF THE  PUBLISHER.  LEGITIMATE<CTRL-M>USER GROUPS  MAY  REQUEST THIS<CTRL-M>PERMISSION.<CTRL-M><CTRL-M>    ----------//----------<CTRL-M>