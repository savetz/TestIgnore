100  REM <CTRL-J><CTRL-J>PROGRAM TITLE<CTRL-J>
110  GOTO 400
200  HTAB (22 -( LEN(H$)/2)): PRINT H$: RETURN 
300  VTAB 9:H$ = "D I E T   A I D": GOSUB 200: RETURN 
400  POKE 768,173: POKE 769,48: POKE 770,192: POKE 771,136: POKE 772,208: POKE 773,4: POKE 774,198: POKE 775,7: POKE 776,240: POKE 777,8: POKE 778,202: POKE 779,208: POKE 780,246: POKE 781,166: POKE 782,6: POKE 783,76: POKE 784,0: POKE 785,3: POKE 786,96: POKE 787,0
410  HOME : INVERSE : FOR H = 1 TO 19: VTAB 1: HTAB 21 -H: PRINT " ";: HTAB 20 +H: PRINT " ";: VTAB 18: HTAB 21 -H: PRINT " ";: HTAB 20 +H: PRINT " ";: NEXT 
420  FOR I = 1 TO 8: VTAB 10 -I: HTAB 1: PRINT " ";: HTAB 40: PRINT " ";: VTAB 9 +I: HTAB 1: PRINT " ";: HTAB 40: PRINT " ";: NEXT 
430  VTAB 2: HTAB 2: PRINT " ";: HTAB 39: PRINT " ": VTAB 17: HTAB 2: PRINT " ";: HTAB 39: PRINT " ";: NORMAL 
440  GOSUB 300
500  POKE 6,222: POKE 7,77: CALL 768: FOR W = 1 TO 200: NEXT : POKE 6,222: POKE 7,30: CALL 768: POKE 6,222: POKE 7,30: CALL 768: POKE 6,222: POKE 7,30: CALL 768: POKE 6,149: POKE 7,212: CALL 768
600 WD$ = "                                        ... AN AWARD-WINNING PROGRAM PRESENTED BY PUBLIC DOMAIN SOFTWARE ON FILE FOR THE USE OF THE PATRONS OF THIS LIBRARY ............. PRESS ANY KEY TO CONTINUE .........."
610 Q$ = "----------------------------------------":Q = 1: NORMAL : VTAB 21: HTAB 1: PRINT Q$: PRINT Q$;: POKE  -16368,0:L =  LEN(WD$):W = 75
620  IF  PEEK( -16384) >127  THEN  POKE  -16368,0: HOME : NORMAL : RUN 1000
630 BN$ =  LEFT$(WD$,40): IF L >166  THEN WD$ =  MID$ (WD$,2,L -1):L = L -1: GOTO 650
640 WD$ =  MID$ (WD$,2,L -1) + LEFT$(WD$,1)
650  VTAB 22: HTAB 1: PRINT BN$: FOR WW = 1 TO W: NEXT : GOTO 620
999  TEXT : HOME : VTAB 10: HTAB 6: PRINT "LOADING SELECT-A-PROGRAM MENU": PRINT : HTAB 13: PRINT "PLEASE WAIT ...":D$ =  CHR$(13) + CHR$(4): PRINT D$"RUN MENU": REM <CTRL-M><CTRL-J><CTRL-J>         END OF TITLE<CTRL-J>
1000  DIM A$(5),TM(10),N$(30)
1010 V = 0: RESTORE : HOME 
1020  DATA "SLEEPING  ","SITTING   ","STANDING  ","WALKING   ","EXERCISE  "
1030  PRINT "          DIET PLANNING PROGRAM": PRINT : PRINT : PRINT : PRINT : PRINT 
1040  INPUT "DO YOU WANT BACKGROUND DATA (Y OR N)";Z$
1050  IF  LEFT$(Z$,1) = "Y"  THEN  GOSUB 2330
1060  HOME : GOSUB 2620
1070  PRINT  TAB( 9)"PHYSICAL REQUIREMENTS"
1080  GOSUB 2620
1090  PRINT 
1100  PRINT "SELECT ENGLISH OR METRIC MEASUREMENT": INPUT "(E OR M): ";V$
1110  IF V$ < >"E"  AND V$ < >"M"  THEN  GOSUB 2680: GOTO 1100
1120 VM =  VAL("M"):VE =  VAL("E"):VV =  VAL(V$)
1130  IF VV < >VM  OR VE  THEN  GOTO 1100
1140  IF  LEFT$(V$,1) = "M"  THEN V = 1: REM  SET METRIC FLAG
1150  PRINT : INPUT "ARE YOU MALE OR FEMALE? (M OR F): ";SX$
1160 S$ =  LEFT$(SX$,1): IF S$ < >"M"  AND S$ < >"F"  THEN  GOSUB 2680: GOTO 1150
1170  PRINT : INPUT "WHAT IS YOUR AGE IN YEARS? ";A
1180  IF A < = 0  OR A >99  THEN  GOSUB 2680: GOTO 1170
1190  PRINT : PRINT "WHAT IS YOUR HEIGHT IN ";
1200  IF V = 1  THEN  INPUT "CENTIMETERS? ";HT: GOTO 1220
1210  INPUT "INCHES? ";HT
1220  IF (V = 1  AND (HT >245  OR HT <1))  OR (V = 0  AND (HT >96  OR HT <1))  THEN  GOSUB 2680: GOTO 1190
1230  PRINT 
1240  PRINT "WHAT IS YOUR PRESENT WEIGHT IN "
1250  IF V = 1  THEN  INPUT "KILOGRAMS? ";WT: GOTO 1270
1260  INPUT "POUNDS? ";WT
1270  IF WT <1  THEN  GOSUB 2680: GOTO 1240
1280  IF WT = 0  THEN 1240
1290  IF V = 1  THEN HT = HT *.3937:WT = WT *2.2: REM  CONVERT TO ENGLISH
1300 K = .010101:KC = WT/154
1310  PRINT : INPUT "HIT RETURN TO CONTINUE";Z$: HOME 
1320  REM  CALCULATE SURFACE AREA DUBOIS METHOD
1330 SA = (WT ^.425) *(HT ^.725) *K
1340  REM  CALCULATE BASAL METABOLIC RATE 
1350  IF SX$ = "F"  THEN 1380
1360 BT =  -.08461538 *A +39.8: IF A <20  THEN BT =  -.577569 *A +54.143874: GOTO 1390
1370  GOTO 1390
1380 BT =  -.08076923 *A +37.576923: IF A <20  THEN BT =  -.635870 *A +51.669203
1390 BM = BT *SA
1400  GOSUB 2620: REM  FIND CALORIC REQUIREMENTS
1410  PRINT  TAB( 12)"ACTIVITY PROFILE"
1420  GOSUB 2620
1430  PRINT "HOW MANY HOURS DO YOU SPEND:"
1440  GOSUB 2630
1450 TT = 0: FOR I = 1 TO 5:TT = TT +TM(I): NEXT : IF TT = 24  THEN 1590
1460  HOME 
1470  PRINT "PLEASE ADJUST YOUR SELECTIONS SO THEY": PRINT "TOTAL 24 HOURS:": PRINT 
1480  PRINT "ITEM","ACTIVITY": PRINT 
1490  FOR I = 1 TO 5: PRINT "  "I,;: GOSUB 1500: GOTO 1560
1500  IF I = 1  THEN  PRINT "SLEEPING ";
1510  IF I = 2  THEN  PRINT "SITTING  ";
1520  IF I = 3  THEN  PRINT "STANDING ";
1530  IF I = 4  THEN  PRINT "WALKING  ";
1540  IF I = 5  THEN  PRINT "EXERCISE ";
1550  RETURN 
1560  PRINT  TAB( 20);: PRINT TM(I): NEXT 
1570  PRINT : PRINT "DAILY ACTIVITY TOTAL IS ";TT;" HOURS"
1580  PRINT : GOTO 1430
1590  REM  CALCULATE CALORIC REQUIREMENTS
1600  GOSUB 1620
1610  GOTO 1680
1620 CT(1) = TM(1) *BM
1630 CT(2) = TM(2) *(BM +(85 -BM) *KC)
1640 CT(3) = TM(3) *(BM +(150 -BM) *KC)
1650 CT(4) = TM(4) *(BM +(240 -BM) *KC)
1660 CT(5) = TM(5) *(BM +(350 -BM) *KC)
1670  RETURN 
1680  PRINT : INPUT "HIT RETURN TO CONTINUE";Z$: HOME 
1690  FOR I = 1 TO 5:CT = CT +CT(I): NEXT 
1700  PRINT "HERE ARE YOUR DAILY CALORIC REQUIRE-"
1710  PRINT "MENTS.  THE TABLE REPRESENTS YOUR CAL-"
1720  PRINT "ORIC REQUIREMENTS BASED ON YOUR ACTI-"
1730  PRINT "VITY PROFILE.  AGE, WEIGHT, HEIGHT AND"
1740  PRINT "SEX ARE USED TO ESTIMATE YOUR BASAL"
1750  PRINT "METABOLIC RATE (SLEEPING)."
1760  PRINT : PRINT "ACTIVITY","HOURS","CALORIES"
1770  RESTORE 
1780  FOR I = 0 TO 4: READ A$(I): NEXT I
1790  FOR I = 0 TO 4: PRINT A$(I), INT(TM(I +1)), INT(CT(I +1)): NEXT I
1800 TT = 0:TC = 0: FOR I = 1 TO 5:TT = TM(I) +TT:TC = CT(I) +TC: NEXT 
1810  PRINT ,"-----","-----"
1820  PRINT "TOTALS", INT(TT), INT(TC)
1830  PRINT :V$ = "(LBS)": IF V = 1  THEN V$ = "(KGS)"
1840  INPUT "HIT RETURN TO CONTINUE";Z$
1850  HOME : GOSUB 2620: REM   DIET PLANNING 
1860  PRINT  TAB( 9)"DIET PLANNING SCHEDULE"
1870  GOSUB 2620
1880  PRINT : PRINT "YOU #X7M4D CONSULT YOUR PHYSICIAN"
1890  PRINT "BEFORE YOU UNDERTAKE ANY DIET PROGRAM."
1900  PRINT "THE FOLLOWING DIET PLAN IS FOR HEALTHY"
1910  PRINT "INDIVIDUALS, FREE OF HEART DISEASE,"
1920  PRINT "DIABETES, THYROID DISORDERS OR CHRONIC"
1930  PRINT "OBESITY.": PRINT 
1940  PRINT "HOW MUCH WEIGHT ";V$;" DO YOU WANT TO": INPUT "LOSE? ";WL
1950  IF WL <1  THEN  GOSUB 2680: GOTO 1940
1960  IF V = 1  THEN WL = WL *2.2
1970  PRINT : INPUT "HOW MANY WEEKS DO YOU WANT TO TAKE? ";LL
1980  IF LL <1  THEN  GOSUB 2680: GOTO 1970
1990  IF WL/LL < = 5  THEN 2040
2000  PRINT "*** WEIGHT LOSS GREATER THAN ";
2010  IF V = 0  THEN  PRINT "5 LBS"
2020  IF V = 1  THEN  PRINT "2.27 KGS"
2030  PRINT "/WEEK IS NOT RECOMMENDED.": PRINT : GOTO 1940
2040 CC =  ABS(WL/LL) *500:AC = TC -CC
2050  HOME 
2060  PRINT "YOUR CURRENT DAILY CALORIE REQUIREMENT"
2070  PRINT "IS ";TC" CALORIES.  TO ACHIEVE THE"
2080  PRINT "WEIGHT LOSS YOU DESIRE, YOU MUST ALLOW"
2090  PRINT "YOURSELF A NUTRITIONAL PROGRAM PROVID-"
2100  PRINT "ING ";AC;" CALORIES DAILY. YOU MAY"
2110  PRINT "ACCOMPLISH THIS BY DECREASING YOUR IN-"
2120  PRINT "TAKE BY ";CC;" CALORIES OR BY"
2130  PRINT "INCREASING YOUR DAILY ACTIVITIES BY"
2140  PRINT "THE SAME AMOUNT."
2150  PRINT : PRINT "OK, HERE IS YOUR DIET PLANNING SCHEDULE."
2160  PRINT : INPUT "HIT RETURN TO CONTINUE";Z$
2170  HOME : PRINT :V$ = " LBS": IF V = 1  THEN V$ = " KGS"
2180  FOR I = 1 TO LL
2190  PRINT "WEEK #" TAB( 10)"WEIGHT" TAB( 30)"CALORIES"
2200 WT = WT -(WL/LL):KC = WT/154
2210 SA = (WT ^.425) *(HT ^.725) *K
2220 BM = BT *SA
2230  GOSUB 1620
2240 CT = 0: FOR J = 1 TO 5:CT = CT +CT(J): NEXT J
2250 AC = CT -CC
2260 WT = ( INT(WT *100 +.5))/100:AC =  INT(AC *100 +.5)/100
2270  IF V = 1  THEN WT = WT/2.2
2280  PRINT I; TAB( 10);WT;V$; TAB( 30);AC: PRINT : IF V = 1  THEN WT = WT *2.2
2290  NEXT I
2300  PRINT : INPUT "DO YOU WANT TO RUN ANOTHER (Y OR N)";Z$
2310  IF  LEFT$(Z$,1) = "Y"  THEN 1010
2320  GOTO 999
2330  REM  BACKGROUND INFORMATION             
2340  HOME : PRINT "   ENERGY DERIVED FROM FOOD ULTIMATELY  APPEARS AS HEAT.  THEREFORE, A UNIT OF  HEAT IS VERY CONVENIENT FOR MEASURING   THE ENERGY EXCHANGES OF THE HUMAN BODY."
2350  PRINT "THE HEAT UNIT CHOSEN IS THE CALORIE,    WHICH REPRESENTS THE AMOUNT OF HEAT     NEEDED TO RAISE THE TEMPERATURE OF ONE  KILOGRAM OF WATER ONE DEGREE CELSIUS."
2360  PRINT "THE ENERGY VALUE OF FOOD AND THE ENERGY REQUIREMENTS OF MAN ARE USUALLY         EXPRESSED AS CALORIE EQUIVALENTS."
2370  PRINT 
2380  PRINT "   A CONSIDERABLE AMOUNT OF ENERGY IS   REQUIRED BY THE BODY JUST TO MAINTAIN   ITS NORMAL FUNCTIONS UNDER BASAL        CONDITIONS (FASTING AND COMPLETE REST)."
2390  PRINT "THIS MINIMUM ENERGY REQUIREMENT IS      EXPRESSED AS CALORIES PER SQUARE METER  OF BODY SURFACE PER HOUR, AND IS        POPULARLY KNOWN AS YOUR BASAL METABOLISMRATE (BMR)."
2400  PRINT : INPUT "     PRESS RETURN FOR NEXT PAGE...";XX$
2410  HOME : PRINT "   THE BASAL ENERGY REQUIREMENTS OF     HUMANS ARE WELL KNOWN, AS ARE THE ENERGYREQUIREMENTS FOR VARIOUS COMMON DAILY   ACTIVITIES.  AS A STANDARD OF MEASURE,"
2420  PRINT "THE FOLLOWING TABLE CONTAINS THE ENERGY REQUIREMENTS PER HOUR FOR A 154 LB.     (70 KG.) MAN:"
2430  PRINT : PRINT "ACTIVITY" TAB( 12)"CALORIES/HOUR"
2440  PRINT : PRINT "SLEEPING"," 70"
2450  PRINT "SITTING"," 85"
2460  PRINT "STANDING","150"
2470  PRINT "WALKING 2MI/HR","240"
2480  PRINT "CLIMBING","350"
2490  PRINT "JOGGING","420"
2500  PRINT "SWIMMING","650"
2510  VTAB 22: INPUT "     PRESS RETURN FOR NEXT PAGE...";XX$
2520  HOME : PRINT "   MANY PEOPLE TODAY ARE PREOCCUPIED    WITH DIETING AND NUTRITION.  FAD DIETS, WHICH LIMIT INTAKE OF FATS, PROTEINS ANDCARBOHYDRATES ARE ALWAYS IN VOGUE.      HOWEVER, YOUR BODY RESPONDS BEST TO A"
2530  PRINT "BALANCED DIET, ONE THAT OFFERS PROPER   AMOUNTS OF THE VITAMINS AND MINERALS    NECESSARY FOR GOOD HEALTH."
2540  PRINT : PRINT "   IT IS IMPORTANT THAT YOU CONSULT A   PHYSICIAN BEFORE YOU EMBARK ON ANY DIET AS DIETING CAN SERIOUSLY AFFECT YOUR    HEALTH.  A PHYSICIAN CAN SUGGEST THE    BEST DIET FOR YOU BASED ON YOUR CURRENT STATE OF HEALTH."
2550  VTAB 22: INPUT "     PRESS RETURN FOR NEXT PAGE...";XX$
2560  HOME : PRINT "   THIS PROGRAM USES YOUR SEX, AGE,     HEIGHT AND WEIGHT TO COMPUTE YOUR BASAL METABOLIC RATE (BMR).  YOUR BMR IS THEN USED AS A STANDARD OF MEASUREMENT TO    ESTIMATE YOUR DAILY CALORIC REQUIREMENTS";
2570  PRINT "FOR THE ACTIVITIES OF SLEEPING, SITTING,STANDING WALKING AND EXERCISING."
2580  PRINT : PRINT "   ONCE YOUR TOTAL DAILY CALORIC        REQUIREMENTS ARE KNOWN, YOU WILL BE     GIVEN THE OPTION OF SELECTING A DIET    PLAN TO LOSE WEIGHT."
2590  PRINT : PRINT "   STARVATION DIETS ARE NOT RECOMMENDED.YOU SHOULD CHOOSE A PLAN THAT ALLOWS FORA STEADY WEIGHT LOSS OF ABOUT TWO POUNDSOR ONE KILOGRAM PER WEEK."
2600  VTAB 22: INPUT "   PRESS RETURN TO START THE PROGRAM...";XX$
2610  HOME : RETURN 
2620  FOR I = 1 TO 39: PRINT "-";: NEXT : PRINT : RETURN 
2630  RESTORE : FOR I = 0 TO 4
2640  READ A$(I): NEXT I
2650  FOR I = 0 TO 4: PRINT ,A$(I);: INPUT TM(I +1)
2660  NEXT I
2670  RETURN 
2680  PRINT "SORRY! TRY A MORE REALISTIC ANSWER."
2690  FOR X = 1 TO 2000: NEXT : VTAB  PEEK(37) -2: CALL  -958: RETURN 
60660  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>          <CTRL-M><CTRL-M>   -------- NOTICE --------<CTRL-M><CTRL-M> ALL ENHANCEMENTS TO THIS PRO-<CTRL-M>GRAM   ARE   COPYRIGHTED  1985<CTRL-M>PUBLIC  DOMAIN RESEARCH.  ANY-<CTRL-M>ONE WISHING TO DISTRIBUTE THIS<CTRL-M>MODIFIED  PROGRAM   MUST  HAVE
63999  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>THE EXPRESSED WRITTEN  CONSENT<CTRL-M>OF THE  PUBLISHER.  LEGITIMATE<CTRL-M>USER GROUPS  MAY  REQUEST THIS<CTRL-M>PERMISSION.<CTRL-M><CTRL-M>    ----------//----------<CTRL-M>