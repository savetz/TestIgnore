100  REM <CTRL-J><CTRL-J>PROGRAM TITLE<CTRL-J>
110  GOTO 400
200  HTAB (22 -( LEN(H$)/2)): PRINT H$: RETURN 
300  VTAB 7:H$ = "J A N E ' S   T I M E R": GOSUB 200
310  VTAB 11:H$ = "BY EVERETT COLLIER": GOSUB 200: RETURN 
400  POKE 768,173: POKE 769,48: POKE 770,192: POKE 771,136: POKE 772,208: POKE 773,4: POKE 774,198: POKE 775,7: POKE 776,240: POKE 777,8: POKE 778,202: POKE 779,208: POKE 780,246: POKE 781,166: POKE 782,6: POKE 783,76: POKE 784,0: POKE 785,3: POKE 786,96: POKE 787,0
410  HOME : INVERSE : FOR H = 1 TO 19: VTAB 1: HTAB 21 -H: PRINT " ";: HTAB 20 +H: PRINT " ";: VTAB 18: HTAB 21 -H: PRINT " ";: HTAB 20 +H: PRINT " ";: NEXT 
420  FOR I = 1 TO 8: VTAB 10 -I: HTAB 1: PRINT " ";: HTAB 40: PRINT " ";: VTAB 9 +I: HTAB 1: PRINT " ";: HTAB 40: PRINT " ";: NEXT 
430  VTAB 2: HTAB 2: PRINT " ";: HTAB 39: PRINT " ": VTAB 17: HTAB 2: PRINT " ";: HTAB 39: PRINT " ";: NORMAL 
440  GOSUB 300
500  POKE 6,222: POKE 7,77: CALL 768: FOR W = 1 TO 200: NEXT : POKE 6,222: POKE 7,30: CALL 768: POKE 6,222: POKE 7,30: CALL 768: POKE 6,222: POKE 7,30: CALL 768: POKE 6,149: POKE 7,212: CALL 768
600 WD$ = "                                        ... AN AWARD-WINNING PROGRAM PRESENTED BY PUBLIC DOMAIN SOFTWARE ON FILE FOR THE USE OF THE PATRONS OF THIS LIBRARY ............. PRESS ANY KEY TO CONTINUE .........."
610 Q$ = "----------------------------------------":Q = 1: NORMAL : VTAB 21: HTAB 1: PRINT Q$: PRINT Q$;: POKE  -16368,0:L =  LEN(WD$):W = 75
620  IF  PEEK( -16384) >127  THEN  POKE  -16368,0: HOME : NORMAL : RUN 1000
630 BN$ =  LEFT$(WD$,40): IF L >166  THEN WD$ =  MID$ (WD$,2,L -1):L = L -1: GOTO 650
640 WD$ =  MID$ (WD$,2,L -1) + LEFT$(WD$,1)
650  VTAB 22: HTAB 1: PRINT BN$: FOR WW = 1 TO W: NEXT : GOTO 620
999  TEXT : HOME : VTAB 10: HTAB 6: PRINT "LOADING SELECT-A-PROGRAM MENU": PRINT : HTAB 13: PRINT "PLEASE WAIT ...":D$ =  CHR$(13) + CHR$(4): PRINT D$"RUN MENU": REM <CTRL-M><CTRL-J><CTRL-J>         END OF TITLE<CTRL-J>
1000  REM <CTRL-M><CTRL-M><CTRL-M>JANE'S EGG TIMER<CTRL-M>BY EVERETT COLLIER<CTRL-M><CTRL-M>
1105  HOME 
1106  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>          <CTRL-J><CTRL-J>
1110  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>CHANGE LINE 111 TO TUNE CLOCK<CTRL-J><CTRL-J><CTRL-J>
1111 TM = 490
1120  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>          <CTRL-J><CTRL-J><CTRL-J>
1130  GR : COLOR= 15:V = 16
1140  VLIN 1,37 AT 0: VLIN 1,37 AT 39
1150  HLIN 1,38 AT 0: HLIN 1,38 AT 38
1160  PLOT 1,1: PLOT 38,1: PLOT 1,37: PLOT 38,37
1170  POKE  -16368,0
1180  HOME 
1190  VTAB 21: PRINT "    ENTER MINUTES TO BE COUNTED DOWN        IN THE RANGE (1-999) : ";: INPUT "";MN$
1200 MN =  VAL(MN$)
1210  IF MN <1  OR MN >999  THEN 1190
1220  HOME 
1230  GOTO 1500
1240  REM   DRAW LETER SUBROUTINES
1250  IF X = 0  THEN 1270
1260  ON X GOSUB 1280,1290,1300,1310,1320,1330,1340,1350,1360: RETURN 
1270  VLIN V,V +5 AT H: HLIN H,H +3 AT V: VLIN V,V +5 AT H +3: HLIN H,H +3 AT V +5: RETURN 
1280  VLIN V,V +5 AT H +2: RETURN 
1290  HLIN H,H +3 AT V: VLIN V,V +2 AT H +3: HLIN H,H +3 AT V +2: VLIN V +2,V +5 AT H: HLIN H,H +3 AT V +5: RETURN 
1300  HLIN H,H +3 AT V: VLIN V,V +5 AT H +3: PLOT H +2,V +2: HLIN H,H +3 AT V +5: RETURN 
1310  VLIN V,V +2 AT H: HLIN H,H +3 AT V +2: VLIN V,V +5 AT H +3: RETURN 
1320  HLIN H,H +3 AT V: VLIN V,V +2 AT H: HLIN H,H +3 AT V +2: VLIN V +2,V +5 AT H +3: HLIN H,H +3 AT V +5: RETURN 
1330  VLIN V +3,V +4 AT H: GOTO 1320
1340  HLIN H,H +3 AT V: VLIN V,V +5 AT H +3: RETURN 
1350  VLIN V,V +3 AT H +3: GOTO 1330
1360  VLIN V,V +2 AT H: HLIN H,H +3 AT V +2: GOTO 1340
1370  COLOR= 0: VLIN V,V +5 AT H: VLIN V,V +5 AT H +1: VLIN V,V +5 AT H +2: VLIN V,V +5 AT H +3: RETURN : REM    CLEAR DIGIT
1500  REM     INITIALIZE SCREEN
1510 H = 29: GOSUB 1270:H = 24: GOSUB 1270
1520  PLOT 22,17: PLOT 22,20
1530 X =  INT(MN -( INT(MN/10) *10)):H = 17: GOSUB 1240
1540 X =  INT((MN -( INT(MN/100) *100))/10):H = 12: IF X = 0  AND  INT((MN -( INT(MN/1000) *1000))/100) = 0  THEN 1560
1550  GOSUB 1240
1560 X =  INT((MN -( INT(MN/1000) *1000))/100): IF X = 0  THEN 1580
1570 H = 7: GOSUB 1240
1580  VTAB 22: PRINT "        PRESS ANY KEY TO START ";: GET A$: HOME 
1590  PRINT : PRINT : PRINT 
1600  VTAB 22: HTAB 8: PRINT "PRESS ANY KEY TO EXIT EARLY"
2000 MN = MN -1: REM    MINUTES LOOP
2005  FOR DD = 1 TO 2:SS =  PEEK( -16336): NEXT DD
2010 X =  INT(MN -( INT(MN/10) *10)):H = 17: GOSUB 1370: COLOR= 2: GOSUB 1240
2020 X =  INT((MN -( INT(MN/100) *100))/10):H = 12: GOSUB 1370: IF X = 0  AND  INT((MN -( INT(MN/1000) *1000))/100) = 0  THEN 2040
2030  COLOR= 3: GOSUB 1240
2040 X =  INT((MN -( INT(MN/1000) *1000))/100):H = 7: GOSUB 1370: IF X = 0  THEN 2060
2050  COLOR= 2: GOSUB 1240
2060  REM    
3000  REM       SECONDS LOOPS
3010  FOR D1 = 5 TO 0  STEP  -1:X = D1
3020 H = 24: GOSUB 1370
3030  COLOR=  INT( RND(13) *14) +2
3040  GOSUB 1240
3050  FOR D0 = 9 TO 0  STEP  -1:X = D0
3060 H = 29: GOSUB 1370
3070  COLOR=  INT( RND(13) *14) +2
3080  GOSUB 1240
3090  FOR DD = 0 TO TM: NEXT DD
3095  FOR DD = 1 TO 2:SS =  PEEK( -16336): NEXT DD
3100  IF  PEEK( -16384) >127  THEN 3150
3110  NEXT D0
3120  NEXT D1
3130  IF MN < >0  THEN 2000
3140  HOME : VTAB 22: PRINT "          ***  T<CTRL-G>IME I<CTRL-G>S U<CTRL-G>P  ***"
3145  FOR W = 1 TO 255  STEP 5: POKE 6,W: POKE 7,15: CALL 768: NEXT W: FOR W = 255 TO 1  STEP  -5: POKE 6,W: POKE 7,15: CALL 768: NEXT 
3146  FOR W = 1 TO 2500: NEXT 
3150  POKE  -16368,0: HOME 
3160  VTAB 22: PRINT "   START THE TIME OVER AGAIN? (Y/N) "
3170  IF  PEEK( -16384) < = 127  THEN 3170
3180  IF  PEEK( -16384) = 217  THEN 1000
3190  IF  PEEK( -16384) < >206  THEN  PRINT "<CTRL-G><CTRL-G>";: POKE  -16368,0: GOTO 3160
3200  GOTO 999
60660  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>          <CTRL-M><CTRL-M>   -------- NOTICE --------<CTRL-M><CTRL-M> ALL ENHANCEMENTS TO THIS PRO-<CTRL-M>GRAM   ARE   COPYRIGHTED  1985<CTRL-M>PUBLIC  DOMAIN RESEARCH.  ANY-<CTRL-M>ONE WISHING TO DISTRIBUTE THIS<CTRL-M>MODIFIED  PROGRAM   MUST  HAVE
63999  REM <CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>THE EXPRESSED WRITTEN  CONSENT<CTRL-M>OF THE  PUBLISHER.  LEGITIMATE<CTRL-M>USER GROUPS  MAY  REQUEST THIS<CTRL-M>PERMISSION.<CTRL-M><CTRL-M>    ----------//----------<CTRL-M>